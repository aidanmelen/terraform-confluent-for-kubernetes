# confluent_platform_iam_secure

Deploy the Confluent Platform components connected with an AWS MSK cluster over TLS. Authenticate and Authorize with IAM. Please see [aws-msk-iam-auth](https://github.com/aws/aws-msk-iam-auth) for more information.

## Prerequisites

Run Terraform in the `../aws` directory.

<!-- BEGINNING OF PRE-COMMIT-TERRAFORM DOCS HOOK -->

## Example

```hcl
resource "kubernetes_service_account_v1" "confluent_platform" {
  metadata {
    name      = "confluent-platform"
    namespace = module.confluent_platform.namespace
    annotations = {
      "eks.amazonaws.com/role-arn" = data.aws_iam_role.aws_msk_full_access.arn
    }
  }
}

module "confluent_platform" {
  source    = "../../../"
  namespace = var.namespace

  # The Confluent Operator was release in ../aws/confluent_operator.tf
  confluent_operator = {
    create = false
  }

  # Both Kafka and Zookeeper were created with AWS MSK in ../aws/main.tf
  create_zookeeper = false
  create_kafka     = false

  # Confluent Platform components with AWS MSK and SASL/IAM
  create_connect        = true
  create_schemaregistry = true
  create_ksqldb         = true
  create_kafkarestproxy = true
  create_controlcenter  = var.create_controlcenter

  # Configuring a Kafka client to use AWS IAM
  # https://github.com/aws/aws-msk-iam-auth#configuring-a-kafka-client-to-use-aws-iam

  connect = yamldecode(<<-EOF
    spec:
      image:
        application: aidanmelen/cp-server-connect-with-aws-msk-iam-auth:7.2.2
      configOverrides:
        server:
          - "security.protocol=SASL_SSL"
          - "sasl.mechanism=AWS_MSK_IAM"
          - "sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "admin.security.protocol=SASL_SSL"
          - "admin.sasl.mechanism=AWS_MSK_IAM"
          - "admin.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "admin.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "producer.security.protocol=SASL_SSL"
          - "producer.sasl.mechanism=AWS_MSK_IAM"
          - "producer.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "producer.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "consumer.security.protocol=SASL_SSL"
          - "consumer.sasl.mechanism=AWS_MSK_IAM"
          - "consumer.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "consumer.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
      podTemplate:
        serviceAccountName: ${kubernetes_service_account_v1.confluent_platform.metadata[0].name}
      tls:
        autoGeneratedCerts: true
      dependencies:
        kafka:
          bootstrapEndpoint: ${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}
          tls:
            enabled: true
            ignoreTrustStoreConfig: true
        schemaRegistry:
          url: https://schemaregistry.${var.namespace}.svc.cluster.local:8081
          tls:
            enabled: true
    EOF
  )

  ksqldb = yamldecode(<<-EOF
    spec:
      image:
        application: aidanmelen/cp-ksqldb-server-with-aws-msk-iam-auth:7.2.2
      configOverrides:
        server:
          - "security.protocol=SASL_SSL"
          - "sasl.mechanism=AWS_MSK_IAM"
          - "sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "ksql.streams.security.protocol=SASL_SSL"
          - "ksql.streams.sasl.mechanism=AWS_MSK_IAM"
          - "ksql.streams.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "ksql.streams.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
      podTemplate:
        serviceAccountName: ${kubernetes_service_account_v1.confluent_platform.metadata[0].name}
      tls:
        # https://docs.confluent.io/operator/current/co-troubleshooting.html#issue-ksqldb-cannot-use-auto-generated-certificates-for-ccloud
        autoGeneratedCerts: false
        secretRef: ${kubernetes_secret_v1.ca_pair_sslcerts.metadata[0].name}
      dependencies:
        kafka:
          bootstrapEndpoint: ${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}
          tls:
            enabled: true
            ignoreTrustStoreConfig: true
        schemaRegistry:
          url: https://schemaregistry.${var.namespace}.svc.cluster.local:8081
          tls:
            enabled: true
    EOF
  )

  # MSK does not support the Confluent Metrics Reporter; therefore, Grafana dashboards are a better alternative.
  # https://grafana.com/grafana/dashboards/11773-confluent-open-source/
  # https://grafana.com/grafana/dashboards/16082-msk-overview/
  controlcenter = yamldecode(<<-EOF
    spec:
      image:
        application: aidanmelen/cp-enterprise-control-center-with-aws-msk-iam-auth:7.2.2
      configOverrides:
        server:
          - "security.protocol=SASL_SSL"
          - "sasl.mechanism=AWS_MSK_IAM"
          - "sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "confluent.controlcenter.streams.security.protocol=SASL_SSL"
          - "confluent.controlcenter.streams.sasl.mechanism=AWS_MSK_IAM"
          - "confluent.controlcenter.streams.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "confluent.controlcenter.streams.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
      podTemplate:
        serviceAccountName: ${kubernetes_service_account_v1.confluent_platform.metadata[0].name}
      tls:
        autoGeneratedCerts: true
      dependencies:
        kafka:
          bootstrapEndpoint: ${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}
          tls:
            enabled: true
            ignoreTrustStoreConfig: true
        schemaRegistry:
          url: https://schemaregistry.${var.namespace}.svc.cluster.local:8081
          tls:
            enabled: true
        ksqldb:
        - name: ksql-dev
          url: https://ksqldb.${var.namespace}.svc.cluster.local:8088
          tls:
            enabled: true
        connect:
        - name: connect-dev
          url:  https://connect.${var.namespace}.svc.cluster.local:8083
          tls:
            enabled: true
    EOF
  )

  schemaregistry = yamldecode(<<-EOF
    spec:
      image:
        application: aidanmelen/cp-schema-registry-with-aws-msk-iam-auth:7.2.2
      configOverrides:
        server:
          - "security.protocol=SASL_SSL"
          - "sasl.mechanism=AWS_MSK_IAM"
          - "sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "kafkastore.security.protocol=SASL_SSL"
          - "kafkastore.sasl.mechanism=AWS_MSK_IAM"
          - "kafkastore.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "kafkastore.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "kafkastore.bootstrap.servers=${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}"
      podTemplate:
        serviceAccountName: ${kubernetes_service_account_v1.confluent_platform.metadata[0].name}
      tls:
        autoGeneratedCerts: true
      dependencies:
        kafka:
          bootstrapEndpoint: ${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}
          tls:
            enabled: true
            ignoreTrustStoreConfig: true
    EOF
  )

  kafkarestproxy = yamldecode(<<-EOF
    spec:
      image:
        application: aidanmelen/cp-kafka-rest-with-aws-msk-iam-auth:7.2.2
      configOverrides:
        server:
          - "security.protocol=SASL_SSL"
          - "sasl.mechanism=AWS_MSK_IAM"
          - "sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
          - "client.security.protocol=SASL_SSL"
          - "client.sasl.mechanism=AWS_MSK_IAM"
          - "client.sasl.jaas.config=software.amazon.msk.auth.iam.IAMLoginModule required;"
          - "client.sasl.client.callback.handler.class=software.amazon.msk.auth.iam.IAMClientCallbackHandler"
      podTemplate:
        serviceAccountName: ${kubernetes_service_account_v1.confluent_platform.metadata[0].name}
      tls:
        autoGeneratedCerts: true
      dependencies:
        kafka:
          bootstrapEndpoint: ${data.aws_msk_cluster.msk.bootstrap_brokers_sasl_iam}
          tls:
            enabled: true
            ignoreTrustStoreConfig: true
        schemaRegistry:
          url: https://schemaregistry.${var.namespace}.svc.cluster.local:8081
          tls:
            enabled: true
    EOF
  )
}
```

## Requirements

| Name | Version |
|------|---------|
| <a name="requirement_terraform"></a> [terraform](#requirement\_terraform) | >= 0.14.8 |
| <a name="requirement_aws"></a> [aws](#requirement\_aws) | >= 3.72 |
| <a name="requirement_helm"></a> [helm](#requirement\_helm) | >= 2.0.0 |
| <a name="requirement_kubernetes"></a> [kubernetes](#requirement\_kubernetes) | >= 2.12.1 |
| <a name="requirement_tls"></a> [tls](#requirement\_tls) | >= 4.0.1 |
## Modules

| Name | Source | Version |
|------|--------|---------|
| <a name="module_confluent_platform"></a> [confluent\_platform](#module\_confluent\_platform) | ../../../ | n/a |
## Inputs

| Name | Description | Type | Default | Required |
|------|-------------|------|---------|:--------:|
| <a name="input_aws_region"></a> [aws\_region](#input\_aws\_region) | The AWS region name. | `string` | `"us-west-2"` | no |
| <a name="input_create_controlcenter"></a> [create\_controlcenter](#input\_create\_controlcenter) | Controls if the ControlCenter component of the Confluent Platform should be created. | `bool` | `true` | no |
| <a name="input_name"></a> [name](#input\_name) | The project name. | `string` | `"hybrid-aws-msk"` | no |
| <a name="input_namespace"></a> [namespace](#input\_namespace) | The namespace to release the Confluent Platform into. | `string` | `"confluent"` | no |
## Outputs

No outputs.
<!-- END OF PRE-COMMIT-TERRAFORM DOCS HOOK -->
